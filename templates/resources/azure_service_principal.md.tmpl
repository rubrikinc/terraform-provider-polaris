---
layout: ""
page_title: "polaris_azure_service_principal Resource - terraform-provider-polaris"
description: |-

---

# polaris_azure_service_principal (Resource)

## Manage Permissions

Polaris require permissions to operate and for Azure this is handled by a service principal. As new features are added
to Polaris the permissions changes. When the status of a subscription feature is `missing-permissions` the permissions
of the service principal must be updated for the feature to continue to function. This can be managed automatically by
Terraform using the the [azurerm](https://registry.terraform.io/providers/hashicorp/azurerm/latest) provider:

```terraform
data "polaris_azure_permissions" "default" {
  features = [
    "cloud-native-protection",
    "exocompute",
  ]
}

resource "azurerm_role_definition" "default" {
  name  = "terraform"
  scope = data.azurerm_subscription.default.id

  permissions {
    actions          = data.polaris_azure_permissions.default.actions
    data_actions     = data.polaris_azure_permissions.default.data_actions
    not_actions      = data.polaris_azure_permissions.default.not_actions
    not_data_actions = data.polaris_azure_permissions.default.not_data_actions
  }
}

resource "azurerm_role_assignment" "default" {
  principal_id       = "9e7f3952-1fc1-11ec-b57a-972144d12d97"
  role_definition_id = azurerm_role_definition.default.role_definition_resource_id
  scope              = data.azurerm_subscription.default.id
}

resource "polaris_azure_service_principal" "default" {
  sdk_auth         = "${path.module}/sdk-service-principal.json"
  tenant_domain    = "mydomain.onmicrosoft.com"
  permissions_hash = data.polaris_azure_permissions.default.hash

  depends_on = [
    azurerm_role_definition.default,
    azurerm_role_assignment.default,
  ]
}
```

When the permissions for a feature changes the `polaris_azure_permissions` data source will reflect this generating a
diff for the `azure_role_definition` and the `polaris_azure_service_principal` resources. Applying the diff will first
update the permissions of the service principal's role definition and then notify Polaris about the update. 

## Example Usage

{{tffile "examples/resources/polaris_azure_service_principal/resource.tf"}}

{{ .SchemaMarkdown | trimspace }}
